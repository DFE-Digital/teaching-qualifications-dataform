config {
    type: "table",
    description: "CCBL search data joined with user data to allow deep dives by organisation name",
    columns: {
        id: "id of each search",
        created_at: "Date search was made",
        dsi_user_id: "ID of the organisation",
        result_returned: "true or false flag of search results generated",
        organisation_name: "name of organisation",
        role_code: "the role of the organisation"

    }
}

SELECT
  id,
  created_at,
  a.dsi_user_id,
  result_returned,
  organisation_name,
  role_code
FROM
  ${ref("search_logs_latest_ccbl_entity_tables")} AS a
LEFT JOIN (
  WITH
    ranked_users AS (
    SELECT
      dsi_user_id,
      organisation_name,
      role_code,
      ROW_NUMBER() OVER (PARTITION BY dsi_user_id ORDER BY organisation_name) AS rn
    FROM
      ${ref("dsi_user_sessions_latest_ccbl_entity_tables")})
SELECT
    dsi_user_id,
    organisation_name,
    role_code
  FROM
    ranked_users
  WHERE
    rn = 1) AS b
ON
  a.dsi_user_id = b.dsi_user_id
